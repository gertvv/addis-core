PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX dataset: <http://trials.drugis.org/datasets/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX entity: <http://trials.drugis.org/entities/>
PREFIX concept: <http://trials.drugis.org/concepts/>
PREFIX instance: <http://trials.drugis.org/instances/>
PREFIX graph: <http://trials.drugis.org/graphs/>

SELECT DISTINCT
?graph
?studyName
?drug
?interventionLabel
?drugInstance
?outcomeInstance
?outcomeTypeUid
?outcomeInstanceLabel
?arm
?armLabel
?mean
?stdDev
?count
?sampleSize
WHERE {
  GRAPH ?graph {
    ?study rdfs:label ?studyName .
    ?study ontology:has_outcome ?outcomeInstance .
    ?outcomeInstance ontology:of_variable [
      owl:sameAs concept:$outcomeUid
    ] .
    ?outcomeInstance a ?outcomeTypeUid .
    ?outcomeInstance rdfs:label ?outcomeInstanceLabel .
    $interventionUnion .
  }
  GRAPH ?graph {
    ?drugInstance owl:sameAs ?drug .
    ?drugInstance rdfs:label ?interventionLabel .
    ?study ontology:has_arm ?arm .
    ?study ontology:has_primary_epoch ?epoch .
    ?activity a ontology:TreatmentActivity ;
      ontology:has_activity_application [
        ontology:applied_to_arm ?arm ;
        ontology:applied_in_epoch ?epoch
      ] ;
      ontology:has_drug_treatment [
        ontology:treatment_has_drug ?drugInstance
      ] .

    ?epoch rdfs:label ?epochLabel .
    ?arm rdfs:label ?armLabel .

    ?measurementMoment
      ontology:relative_to_epoch ?epoch ;
      ontology:relative_to_anchor ontology:anchorEpochEnd ;
      ontology:time_offset "PT0S"^^xsd:duration .

    ?measurement
      ontology:of_moment ?measurementMoment ;
      ontology:of_outcome ?outcomeInstance ;
      ontology:of_arm ?arm .

    OPTIONAL {
      ?measurement
        ontology:mean ?mean ;
        ontology:standard_deviation ?stdDev ;
        ontology:sample_size ?sampleSize .
    }

    OPTIONAL {
      ?measurement
        ontology:count ?count ;
        ontology:sample_size ?sampleSize .
    } .
    {
      SELECT ?study ?arm
      WHERE {
       ?activity a ontology:TreatmentActivity ;
             ontology:has_activity_application [
               ontology:applied_to_arm ?arm ;
               ontology:applied_in_epoch ?epoch
             ] ;
             ontology:has_drug_treatment [
               ontology:treatment_has_drug ?drugInstance
             ] .
      } GROUP BY ?study ?arm
          HAVING (COUNT(?drugInstance) < 2)
    }
  }
}