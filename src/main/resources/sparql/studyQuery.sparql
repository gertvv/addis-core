PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX dataset: <http://trials.drugis.org/datasets/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX graph: <http://trials.drugis.org/graphs/>

SELECT * WHERE {
  GRAPH ?studyGraphUri {
    ?studyUri a ontology:Study .
    ?studyUri rdfs:label ?label .
    ?studyUri rdfs:comment ?title .
    ?activity
      a ontology:TreatmentActivity ;
      ontology:has_activity_application [
        ontology:applied_to_arm ?armUid
      ] ;
      ontology:has_drug_treatment [
        ontology:treatment_has_drug ?treatmentDrug
      ] .
    ?armUid rdfs:label ?armLabel .
    ?treatmentDrug rdfs:label ?drugLabel .
    ?treatmentDrug owl:sameAs ?drugUid .
    {
      SELECT ?studyUri (group_concat(?outcomeUid; separator = ", ") as ?outcomeUids)
      WHERE {
          ?studyUri ontology:has_outcome ?outcomeInstanceUid .
          ?outcomeInstanceUid ontology:of_variable [
            owl:sameAs ?outcomeUid
          ] .
      } GROUP BY ?studyUri
    }
  }
}
