PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX dataset: <http://trials.drugis.org/datasets/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX graph: <http://trials.drugis.org/graphs/>
PREFIX list: <http://jena.hpl.hp.com/ARQ/list#>
PREFIX instance: <http://trials.drugis.org/instances/>

SELECT DISTINCT
  ?treatmentActivityUid
  ?epochUid
  ?armUid
  ?treatmentActivityType
  ?drugUid
  ?treatmentDrugLabel
  ?minValue
  ?minUnitLabel
  ?minDosingPeriodicity
  ?maxValue
  ?maxUnitLabel
  ?maxDosingPeriodicity
  ?fixedValue
  ?fixedUnitLabel
  ?fixedDosingPeriodicity
WHERE {
  GRAPH graph:$studyUid {
 ?studyUid ontology:has_epochs ?epochs .
     ?studyUid ontology:has_arm ?armUid .
     ?epochs  list:member ?epochUid .
     ?treatmentActivityUid a ?treatmentActivityType .
     ?treatmentActivityUid ontology:has_activity_application [
       ontology:applied_to_arm ?armUid ;
       ontology:applied_in_epoch ?epochUid
     ] .
     OPTIONAL {
       ?treatmentActivityUid ontology:has_drug_treatment ?treatmentNode .
       ?treatmentNode ontology:treatment_has_drug ?drugUid .
       ?drugUid rdfs:label ?treatmentDrugLabel .
       OPTIONAL {
         ?treatmentNode ontology:treatment_min_dose [
             rdf:value ?minValue ;
             ontology:dosingPeriodicity ?minDosingPeriodicity;
             ontology:unit ?minUnit
          ] ;
           ontology:treatment_max_dose [
             ontology:unit ?maxUnit ;
             rdf:value ?maxValue ;
             ontology:dosingPeriodicity ?maxDosingPeriodicity
           ] .
         ?minUnit rdfs:label ?minUnitLabel .
         ?maxUnit rdfs:label ?maxUnitLabel .
       }
       OPTIONAL {
         ?treatmentNode ontology:treatment_dose [
           rdf:value ?fixedValue ;
           ontology:unit ?fixedUnit ;
           ontology:dosingPeriodicity ?fixedDosingPeriodicity
         ] .
         ?fixedUnit rdfs:label ?fixedUnitLabel .
       }
     }
   }
 }