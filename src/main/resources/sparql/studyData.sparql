PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX dataset: <http://trials.drugis.org/datasets/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX graph: <http://trials.drugis.org/graphs/>
PREFIX list: <http://jena.hpl.hp.com/ARQ/list#>
PREFIX instance: <http://trials.drugis.org/instances/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT
  ?outcomeUid
  ?studyDataTypeUri
  ?studyDataTypeLabel
  ?armInstanceUid
  ?armLabel
  ?sampleSize
  ?count
  ?mean
  ?std
  ?categoryLabel
  ?categoryCount
  ?momentUid
  ?relativeToAnchor
  ?timeOffset
  ?duration
  ?relativeToEpochLabel
  {
  GRAPH graph:concepts{
    ?studyDataTypeUri
      a ontology:Variable ;
      rdfs:label ?studyDataTypeLabel .
   }
   GRAPH graph:$studyUid {
     ?study a ontology:Study .
     ?study ontology:has_outcome ?outcomeUid .
     ?outcomeUid
       a ontology:$studyDataType ;
       ontology:of_variable [
         owl:sameAs ?studyDataTypeUri
       ] .
     ?measurementJoinInstance
         ontology:of_outcome ?outcomeUid ;
         ontology:of_arm ?armInstanceUid ;
         ontology:of_moment ?momentUid .
     ?armInstanceUid rdfs:label ?armLabel ;
             a ontology:Arm .
     OPTIONAL {
          ?measurementJoinInstance ontology:sample_size ?sampleSize .
     }
     OPTIONAL {
         ?measurementJoinInstance ontology:count ?count .
     }
     OPTIONAL {
         ?measurementJoinInstance ontology:mean ?mean ;
         ontology:standard_deviation ?std .
     }
     OPTIONAL {
        ?measurementJoinInstance ontology:category_count ?node .
        ?node
          ontology:category ?categoryLabel ;
          ontology:count ?categoryCount .
     }
     OPTIONAL {
     ?momentUid
         ontology:relative_to_epoch ?relativeToEpoch ;
         ontology:relative_to_anchor ?relativeToAnchor ;
         ontology:time_offset ?timeOffset .
     ?relativeToEpoch
         ontology:duration ?duration ;
         rdfs:label ?relativeToEpochLabel ;
         rdf:type ontology:Epoch .
     }
   }
 }
